={
'Лист1'!AF18\ "Статус"\ "№ пары"\ "№ кладовой"\ "площадь м/м"\ "площадь кладовой"\ "общая площадь"\ "цена кв.м.";
ARRAYFORMULA(
IF(
  ('Лист1'!L3:L81="")+('Лист1'!L3:L81="№ м/м");
  ;
  {
    TEXT('Лист1'!L3:L81;"P000")\
    'Лист1'!M3:M81\
    IF('Лист1'!N3:N81<>"";TEXT('Лист1'!N3:N81;"P000");)\
    IF('Лист1'!O3:O81="";;"K12"&RIGHT("0"&REGEXREPLACE('Лист1'!O3:O81;"^12\.";"");2))\
    'Лист1'!P3:P81\
    'Лист1'!Q3:Q81\
    'Лист1'!P3:P81+'Лист1'!Q3:Q81\
    'Лист1'!S3:S81
  }
)
);

ARRAYFORMULA(
IF(
  'Лист1'!A3:A71="";
  ;
  {
    TEXT('Лист1'!A3:A71;"P000")\
    'Лист1'!B3:B71\
    IF('Лист1'!C3:C71<>"";TEXT('Лист1'!C3:C71;"P000");)\
    IF('Лист1'!D3:D71="";;"K13"&RIGHT("0"&REGEXREPLACE('Лист1'!D3:D71;"^13\.";"");2))\
    'Лист1'!E3:E71\
    'Лист1'!F3:F71\
    'Лист1'!E3:E71+'Лист1'!F3:F71\
    'Лист1'!H3:H71
  }
)
);

ARRAYFORMULA(
  IF(
    'Лист1'!X3:X21="";
    ;
    {
      IF('Лист1'!Y3:Y21="";;"K12"&RIGHT("0"&REGEXEXTRACT('Лист1'!Y3:Y21;"^12\.(\d+)");2))\
      'Лист1'!X3:X21\
      IF('Лист1'!X3:X21="";;"" )\
      IF('Лист1'!X3:X21="";;"" )\
      'Лист1'!Z3:Z21\
      IF('Лист1'!X3:X21="";;"" )\
      'Лист1'!Z3:Z21\
      'Лист1'!AA3:AA21
    }
  )
);

ARRAYFORMULA(
  IF(
    'Лист1'!AD3:AD13="";
    ;
    {
      IF('Лист1'!AF3:AF13="";;"K13"&RIGHT("0"&REGEXEXTRACT('Лист1'!AF3:AF13;"^13\.(\d+)");2))\
      'Лист1'!AE3:AE13\
      IF('Лист1'!AD3:AD13="";;"" )\
      IF('Лист1'!AD3:AD13="";;"" )\
      'Лист1'!AG3:AG13\
      IF('Лист1'!AD3:AD13="";;"" )\
      'Лист1'!AG3:AG13\
      'Лист1'!AH3:AH13
    }
  )
)

}
